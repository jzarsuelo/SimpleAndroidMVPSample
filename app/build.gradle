apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "com.jzarsuelo.android.sunshine"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    flavorDimensions 'default'

    productFlavors {
        dev {
            applicationIdSuffix '.dev'
            dimension 'default'
            buildConfigField 'String', 'API_APP_ID', "\"$apiKeys.openWeatherMapDev\""
            buildConfigField 'String', 'apiBaseUri', api.baseUri
            buildConfigField 'String', 'apiVersion', api.version
        }
        prod {
            dimension 'default'
            buildConfigField 'String', 'API_APP_ID', "\"$apiKeys.openWeatherMapProd\""
            buildConfigField 'String', 'apiBaseUri', api.baseUri
            buildConfigField 'String', 'apiVersion', api.version
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // android
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$versions.kotlin"
    implementation "com.android.support:appcompat-v7:$versions.support"
    implementation "com.android.support.constraint:constraint-layout:$versions.constraintLayout"
    implementation "com.android.support:recyclerview-v7:$versions.support"
    implementation "com.android.support:design:$versions.support"

    // libraries
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.okhttp"
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    kapt "com.github.bumptech.glide:compiler:$versions.glide"
    implementation "com.facebook.stetho:stetho:$versions.stetho"

    // test
    testImplementation "junit:junit:$versions.junit"
    androidTestImplementation "com.android.support.test:runner:$versions.runner"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$versions.espresso"
}
